// 颜色stroop效应  以红绿色为例
<item instruction>
	/1="您好，实验开始时，在屏幕的中央会随机出现红色或绿色的汉字，
如果字体颜色是红色，请用左手的食指按“F”键，
如果是绿色，请右手的食指按“J”键"
</item>

<item word>
	/1 = "红"
	/2 = "绿"
</item>
// 定义呈现的刺激内容 可用text可以调用

<text correct>
	/items = ("正确")
	/fontstyle = ("宋体", 20pt)
	/txbgcolor = (transparent)
	/txcolor = (255,0,0)
</text>
// transparent 透明  fontstyel 字体格式

<text error>
	/items = ("错误")
	/fontstyle = ("宋体", 20pt)
	/txbgcolor = (transparent)
	/txcolor = (255,0,0)
</text>

<text instruction>
	/items = instruction
	/fontstyle=("宋体",40pt)
	/txbgcolor=(0,0,0)
	/txcolor=(255,255,255)
	/hjustify = center
</text>
//hjustify 水平对齐方式

<text anykey>
	/items = ("按任意键开始实验")
	/vposition = 70 pct
	/fontstyle = ("宋体", 24pt)
	/txcolor = (255,255,255)
	/txbgcolor = (transparent)
</text>
// vposition 垂直对齐方式 pct百分比

<text green>
	/items = word
	/fontstyle=("宋体",70ptc)
	/txcolor = (0,255,0)
	/txbgcolor = (0,0,0)
</text>

<text red>
	/items = word
	/fontstyle = ("宋体",70ptc)
	/size =(50,50)
	/txcolor = (255,0,0)
	/txbgcolor = (0,0,0)
</text>

//text决定刺激呈现的方式 trialk可以调用text

<trial instruction>
	/validresponse = (anyresponse)
	/stimulustimes = [1=instruction,anykey]
	/recorddata = false
</trial>
// sti [时间=呈现的内容]

<trial greentrial>
	/correctmessage = (correct,500)
	/errormessage = (error,500)
	/validresponse = ("F","J")
	/correctresponse = ("J")
	/pretrialpause = 500
	/stimulustimes = [500 = green]
</trial>
//pretrialpause 间隔时间

<trial redtrial>
	/correctmessage = (correct,500)
	/errormessage = (error,500)
	/pretrialpause = 500
	/validresponse = ("F","J")
	/correctresponse = ("F")
	/stimulustimes = [500 = red]
</trial>
//trail 定义有效的按键反应，正确的按键反应，刺激呈现顺序等信息 block调用

<block instructionblock>
  /screencolor = (0,0,0)
  /trials = [1 = instruction]
</block>


<block redgreenblock>
	/screencolor = (0,0,0)
	/blockfeedback = (meanlatency,correct)
	/trials = [1-12 = noreplace(redtrial, greentrial)]
</block>
//feedback 反应时，正确率  1-12 12个trials noreplace随机呈现
//block 定义属性信息 expt调用

<expt>
  /blocks = [1=instructionblock;2 = redgreenblock]
</expt>