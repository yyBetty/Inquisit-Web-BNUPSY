<item redword>
	/1 = "红"
	/2 = "蓝"
</item>

<item blueword>
	/1 = "红"
	/2 = "蓝"
</item>


<text redwordtxt>
	/items =  redword
	/fontstyle = ("Arial",60ptc)
	/txbgcolor = (0,0,0)
	/txcolor = (255,0,0)
</text>

<text bluewordtxt>
	/items =  blueword
	/ fontstyle = ("Arial",60ptc)
	/txbgcolor = (0,0,0)
	/txcolor = (0,0,255)
</text>

<text correctmsg>
	/items = ("√")
	/fontstyle = ("Arial", 10%)
	/txbgcolor = (transparent)
	/txcolor = (255,255,255)
</text>

<text errormsg>
	/items = ("×")
	/fontstyle = ("Arial", 10%)
	/txbgcolor = (transparent)
	/txcolor = (255,255,255)
</text>

<text fixation>
	/items = ("+")
	/fontstyle = ("Arial", 40ptc)
	/txbgcolor = (transparent)
	/txcolor = (255,255,255)
</text>

<text instructiontxt>
	/hjustify = center
	/items = ("请将左数和右手的食指分别放在键盘的“F”键和“J”键上；在屏幕的中央会随机出现不同颜色的汉字，请判断汉字呈现的颜色(而非汉字的字面意义)；如果是红色的字，请按“F”键，如果是蓝色的字，请按“J”键。")
	/size = (1000,300)
	/fontstyle = ("宋体", 24pt)
	/txbgcolor = (transparent)
	/txcolor = (255,255,255)
</text>

<text anykeytxt>
	/items = ("按任意键开始实验")
	/vposition = 70 pct
	/fontstyle = ("宋体", 24pt)
	/txcolor = (255,255,255)
	/txbgcolor = (transparent)
</text>

**************************************************************************************************************************************************************************
增加几个instruction trial 来呈现指导语

<trial instruction>
	/validresponse = (anyresponse)
	/stimulusframes = [1=instructiontxt,anykeytxt]
	/recorddata = false
</trial>

<trial redwordtrial>
	/correctmessage = (correctmsg,500)
	/errormessage = (errormsg,500)
	/pretrialpause = 500
	/validresponse = ("F","J")
	/correctresponse = ("F")
	/stimulusframes = [1 = fixation; 100 = redwordtxt]
</trial>

<trial bluewordtrial>
	/correctmessage = (correctmsg,500)
	/errormessage = (errormsg,500)
	/pretrialpause = 500
	/validresponse = ("F","J")
	/correctresponse = ("J")
	/stimulusframes = [1 = fixation; 100 = bluewordtxt]
</trial>

**************************************************************************************************************************************************************************
<block redblueblk>
	/screencolor = (0,0,0)
	/blockfeedback = (meanlatency,correct)
	/trials = [1-20 = noreplace(redwordtrial, bluewordtrial)]
</block>

//增加instruction block来呈现指导语

<block instructionblk>
	/screencolor = (0,0,0)
	/trials = [1 = instruction]
</block>


//先呈现指导语，再呈现主体任务。

<expt>
	/ blocks = [1 = instructionblk; 2 = redblueblk]
</expt>

**************************************************************************************************************************************************************************
